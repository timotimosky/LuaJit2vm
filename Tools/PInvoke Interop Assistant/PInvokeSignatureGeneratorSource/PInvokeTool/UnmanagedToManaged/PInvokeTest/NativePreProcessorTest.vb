' Copyright (c) Microsoft Corporation.  All rights reserved.
'The following code was generated by Microsoft Visual Studio 2005.
'The test owner should check each test for validity.
Imports Microsoft.VisualStudio.TestTools.UnitTesting
Imports System
Imports System.Text
Imports System.Collections.Generic
Imports PInvoke.Parser
Imports System.IO


'''<summary>
'''This is a test class for PInvoke.Parser.NativePreProcessor and is intended
'''to contain all PInvoke.Parser.NativePreProcessor Unit Tests
'''</summary>
<TestClass()> _
Public Class PreProcessorEngineTest


    Private testContextInstance As TestContext

    '''<summary>
    '''Gets or sets the test context which provides
    '''information about and functionality for the current test run.
    '''</summary>
    Public Property TestContext() As TestContext
        Get
            Return testContextInstance
        End Get
        Set(ByVal value As TestContext)
            testContextInstance = value
        End Set
    End Property
#Region "Additional test attributes"
    '
    'You can use the following additional attributes as you write your tests:
    '
    'Use ClassInitialize to run code before running the first test in the class
    '
    '<ClassInitialize()>  _
    'Public Shared Sub MyClassInitialize(ByVal testContext As TestContext)
    'End Sub
    '
    'Use ClassCleanup to run code after all tests in a class have run
    '
    '<ClassCleanup()>  _
    'Public Shared Sub MyClassCleanup()
    'End Sub
    '
    'Use TestInitialize to run code before running each test
    '
    '<TestInitialize()>  _
    'Public Sub MyTestInitialize()
    'End Sub
    '
    'Use TestCleanup to run code after each test has run
    '
    '<TestCleanup()>  _
    'Public Sub MyTestCleanup()
    'End Sub
    '
#End Region

    Private Sub VerifyCount(ByVal text As String, ByVal errorCount As Integer, ByVal warningCount As Integer)
        Dim p As New PreProcessorEngine(New PreProcessorOptions)
        p.Process(New TextReaderBag("text", New StringReader(text)))
        Assert.AreEqual(errorCount, p.ErrorProvider.Errors.Count)
        Assert.AreEqual(warningCount, p.ErrorProvider.Warnings.Count)
    End Sub

    Private Function VerifyImpl(ByVal opts As PreProcessorOptions, ByVal before As String, ByVal after As String) As Dictionary(Of String, Macro)
        Dim p As New PreProcessorEngine(opts)
        Dim actual As String = p.Process(New TextReaderBag("before", New StringReader(before)))
        Assert.AreEqual(after, actual)

        Return p.MacroMap
    End Function


    Private Function VerifyNormal(ByVal before As String, ByVal after As String) As Dictionary(Of String, Macro)
        Return VerifyNormal(New List(Of Macro), before, after)
    End Function

    Private Function VerifyNormal(ByVal initialList As List(Of Macro), ByVal before As String, ByVal after As String) As Dictionary(Of String, Macro)
        Dim opts As New PreProcessorOptions()
        opts.InitialMacroList.AddRange(initialList)
        opts.FollowIncludes = False
        Return VerifyImpl(opts, before, after)
    End Function

    Private Function VerifyNoMetadata(ByVal before As String, ByVal after As String) As Dictionary(Of String, Macro)
        Dim opts As New PreProcessorOptions()
        opts.FollowIncludes = True
        Return VerifyImpl(opts, before, after)
    End Function

    Private Sub VerifyMap(ByVal map As Dictionary(Of String, Macro), ByVal ParamArray args() As String)
        For i As Integer = 0 To args.Length - 1 Step 2
            Dim name As String = args(i)
            Dim value As String = args(i + 1)
            Assert.IsTrue(map.ContainsKey(name), "Could not find " & name & " in the macro map")

            Dim macro As Macro = map(name)
            Assert.AreEqual(value, macro.Value)
        Next
    End Sub

    Private Function VerifyMacro(ByVal data As String, ByVal ParamArray args() As String) As Dictionary(Of String, Macro)
        Dim opts As New PreProcessorOptions()
        opts.FollowIncludes = False
        Dim p As New PreProcessorEngine(opts)
        p.Process(New TextReaderBag("foo", New StringReader(data)))
        VerifyMap(p.MacroMap, args)
        Return p.MacroMap
    End Function

    Private Function EvalCond(ByVal list As List(Of Macro), ByVal cond As String) As Boolean
        Dim before As String = _
            "#if " & cond & vbCrLf & _
            "true" & vbCrLf & _
            "#else" & vbCrLf & _
            "false" & vbCrLf & _
            "#endif"
        Dim opts As New PreProcessorOptions()
        opts.InitialMacroList.AddRange(list)
        Dim engine As New PreProcessorEngine(opts)
        Dim val As String = engine.Process(New TextReaderBag("foo", New StringReader(before)))
        Return val.StartsWith("true")
    End Function

    Private Sub VerifyCondTrue(ByVal list As List(Of Macro), ByVal cond As String)
        Assert.IsTrue(EvalCond(list, cond))
    End Sub

    Private Sub VerifyCondFalse(ByVal list As List(Of Macro), ByVal cond As String)
        Assert.IsFalse(EvalCond(list, cond))
    End Sub

    <TestMethod()> _
    Public Sub Conditional1()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#if foo" & vbCrLf & _
            "hello" & vbCrLf & _
            "#endif"
        Dim after As String = "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    <TestMethod()> _
    Public Sub Conditional2()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#if foo" & vbCrLf & _
            "hello" & vbCrLf & _
            "world" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "hello" & vbCrLf & _
            "world" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    <TestMethod()> _
    Public Sub Conditional3()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#if foo" & vbCrLf & _
            "hello" & vbCrLf & _
            "#else" & vbCrLf & _
            "world" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    ''' <summary>
    ''' Hit the else clause
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Conditional4()
        Dim before As String = _
            "#if foo" & vbCrLf & _
            "hello" & vbCrLf & _
            "#else" & vbCrLf & _
            "world" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "world" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Hit the #elseif
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Conditional5()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#if bar" & vbCrLf & _
            "hello" & vbCrLf & _
            "#elseif foo" & vbCrLf & _
            "world" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "world" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    ''' <summary>
    ''' Skip the else when the #elseif is hit
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Conditional6()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#if bar " & vbCrLf & _
            "hello" & vbCrLf & _
            "#elseif foo" & vbCrLf & _
            "world" & vbCrLf & _
            "#else" & vbCrLf & _
            "again" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "world" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    <TestMethod()> _
    Public Sub Conditional7()
        Dim before As String = _
            "#define _PREFAST_" & vbCrLf & _
            "#if !(defined(_midl)) && defined(_PREFAST_)" & vbCrLf & _
            "hello" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "hello" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' make sure that we collapse #     define
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Conditional8()
        Dim before As String = _
                  "#     define foo bar" & vbCrLf & _
                  "#     if bar" & vbCrLf & _
                  "hello" & vbCrLf & _
                  "#    elseif foo" & vbCrLf & _
                  "world" & vbCrLf & _
                  "#    endif"
        Dim after As String = _
            "world" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    <TestMethod()> _
     Public Sub Conditional9()
        Dim before As String = _
                  "#     define foo bar" & vbCrLf & _
                  "#     if defined foo " & vbCrLf & _
                  "hello" & vbCrLf & _
                  "#    else " & vbCrLf & _
                  "world" & vbCrLf & _
                  "#    endif"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar")
    End Sub

    <TestMethod()> _
    Public Sub Conditional10()
        Dim before As String = _
            "#define FOO 1" & vbCrLf & _
            "#if FOO & 1" & vbCrLf & _
            "hello" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "hello" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Conditional11()
        Dim before As String = _
            "#define FOO 1" & vbCrLf & _
            "#if FOO & 2" & vbCrLf & _
            "hello" & vbCrLf & _
            "#endif"
        Dim after As String = String.Empty
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Conditional12()
        Dim before As String = _
            "#define FOO 1" & vbCrLf & _
            "#if FOO | 2" & vbCrLf & _
            "hello" & vbCrLf & _
            "#endif"
        Dim after As String = _
            "hello" & vbCrLf
        VerifyNormal(before, after)
    End Sub


    ''' <summary>
    ''' Simple multiline macro.  
    ''' 
    ''' The hello line will end with a newline because it's the last line in the file.
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Multiline1()
        Dim before As String = _
            "#define foo bar \" & vbCrLf & _
            "baz" & vbCrLf & _
            "hello"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar baz")
    End Sub


    <TestMethod()> _
    Public Sub Multiline2()
        Dim before As String = _
            "#define foo bar \" & vbCrLf & _
            "baz \" & vbCrLf & _
            "again " & vbCrLf & _
            "hello"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar baz again")
    End Sub

    <TestMethod()> _
    Public Sub Multiline3()
        Dim before As String = _
            "#define foo bar \" & vbCrLf & _
            "baz /*foo*/\" & vbCrLf & _
            " " & vbCrLf & _
            "hello"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar baz")
    End Sub

    <TestMethod()> _
    Public Sub Multiline4()
        Dim before As String = _
            "#define foo bar \" & vbCrLf & _
            "baz /*foo*/\" & vbCrLf & _
            " // hello " & vbCrLf & _
            "hello"
        Dim after As String = _
            "hello" & vbCrLf
        Dim map As Dictionary(Of String, Macro)
        map = VerifyNormal(before, after)
        VerifyMap(map, "foo", "bar baz")
    End Sub

    ''' <summary>
    ''' Preprocessor should remove comments
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Comment1()
        Dim before As String = _
            "/* hello */" & vbCrLf & _
            "world" & vbCrLf
        Dim after As String = _
            vbCrLf & "world" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Comment2()
        Dim before As String = _
            "hello" & vbCrLf & _
            "/* hello */" & vbCrLf & _
            "world" & vbCrLf
        Dim after As String = _
            "hello" & vbCrLf & _
            vbCrLf & _
            "world" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Comment3()
        Dim before As String = _
            "// hello */" & vbCrLf & _
            "world" & vbCrLf
        Dim after As String = _
            vbCrLf & "world" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Make sure the preprocessor won't ignore an entire line when it hits
    ''' a comment
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Comment4()
        Dim before As String = _
            "/* hello */ hello" & vbCrLf & _
            "world" & vbCrLf
        Dim after As String = _
            " hello" & vbCrLf & _
            "world" & vbCrLf
        VerifyNormal(before, after)
    End Sub
    ''' <summary>
    ''' Parse out simple macros
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Macro1()
        Dim data As String = _
            "#define foo bar" & vbCrLf & _
            "#define bar foo"
        VerifyMacro(data, "foo", "bar", "bar", "foo")
    End Sub

    ''' <summary>
    ''' Comment in the value
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Macro2()
        Dim data As String = _
            "#define foo /* hello */bar" & vbCrLf & _
            "#define bar foo"
        VerifyMacro(data, "foo", "bar", "bar", "foo")
    End Sub

    ''' <summary>
    ''' Undef the macro
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Macro3()
        Dim data As String = _
            "#define foo bar" & vbCrLf & _
            "#undef foo"
        Dim map As Dictionary(Of String, Macro) = VerifyMacro(data)
        Assert.AreEqual(0, map.Count)
    End Sub

    <TestMethod()> _
    Public Sub Macro4()
        Dim data As String = _
            "#define foo bar" & vbCrLf & _
            "#define /* hollow */ bar foo"
        VerifyMacro(data, "foo", "bar", "bar", "foo")
    End Sub

    ''' <summary>
    ''' Make sure that if the values are just wrapped in a set of () that we don't 
    ''' treat it as a macro method
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Macro5()
        Dim before As String = _
            "#define foo   (1)" & vbCrLf & _
            "foo" & vbCrLf
        Dim after As String = _
            "(1)" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Macro6()
        Dim before As String = _
            "#define foo   ((2)1)" & vbCrLf & _
            "foo" & vbCrLf
        Dim after As String = _
            "((2)1)" & vbCrLf
        VerifyNormal(before, after)
    End Sub


    <TestMethod()> _
    Public Sub Eval1()
        Dim list As New List(Of Macro)
        list.Add(New Macro("foo", "bar"))
        VerifyCondTrue(list, "foo")
    End Sub

    <TestMethod()> _
    Public Sub Eval2()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondFalse(list, "foo")
    End Sub

    ''' <summary>
    ''' Test the "defined" function
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval3()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "defined(m1)")
    End Sub

    ''' <summary>
    ''' Add some parens
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval4()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "(m1)")
    End Sub

    ''' <summary>
    ''' add some ||'s 
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval5()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "foo || m1")
        VerifyCondFalse(list, "foo  || bar")
    End Sub

    ''' <summary>
    ''' Test some and's
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval6()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "m1 && m2")
        VerifyCondFalse(list, "foo && m2")
        VerifyCondTrue(list, "m1 && 1")
    End Sub

    ''' <summary>
    ''' Complex ones
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval7()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "defined(m1) || ab")
        VerifyCondFalse(list, "((m1) || m2) && foo")
    End Sub

    <TestMethod()> _
    Public Sub Eval8()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondFalse(list, "defined(m5)")
    End Sub

    <TestMethod()> _
    Public Sub Eval9()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "true"))
        list.Add(New Macro("m2", "false"))
        list.Add(New Macro("m3", "1"))
        list.Add(New Macro("m4", "0"))
        VerifyCondTrue(list, "!defined(m5)")
    End Sub

    ''' <summary>
    ''' Relational evaluation
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval10()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "5"))
        list.Add(New Macro("m2", "6"))
        VerifyCondTrue(list, "m2 > m1")
        VerifyCondTrue(list, "m2 >= m1")
    End Sub

    ''' <summary>
    ''' Relational operators with hex numbers
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Eval11()
        Dim list As New List(Of Macro)
        list.Add(New Macro("m1", "0x5"))
        list.Add(New Macro("m2", "0x6"))
        VerifyCondTrue(list, "m2 > m1")
        VerifyCondTrue(list, "m2 >= m1")
    End Sub

    ''' <summary>
    ''' Make sure we're replacing defined tokens
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Replace1()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "foo" & vbCrLf
        Dim after As String = _
            "bar" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Replace2()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "baz" & vbCrLf
        Dim after As String = _
            "baz" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' C++ trick for introducing a comment.  Used in the definition for _VARIANT_BOOL
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Replace3()
        Dim before As String = _
            "#define foo /##/ " & vbCrLf & _
            "foo bar" & vbCrLf
        Dim after As String = _
            "// bar" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Replace4()
        Dim before As String = _
            "#define m1(x) x##1 2" & vbCrLf & _
            "m1(5)" & vbCrLf
        Dim after As String = _
            "51 2" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Simple macro method
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method1()
        Dim before As String = _
            "#define foo(x) x" & vbCrLf & _
            "foo(1)"
        Dim after As String = _
            "1" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Couple of different replacements
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method2()
        Dim before As String = _
            "#define foo(x) x" & vbCrLf & _
            "foo(1)" & vbCrLf & _
            "foo(""hello"")" & vbCrLf & _
            "foo(0x5)" & vbCrLf
        Dim after As String = _
            "1" & vbCrLf & _
            """hello""" & vbCrLf & _
            "0x5" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Whitespace junk
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method3()
        Dim before As String = _
            "#define foo(x)                   x" & vbCrLf & _
            "foo(1)" & vbCrLf
        Dim after As String = _
            "1" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Several macros
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method4()
        Dim before As String = _
            "#define foo(x,y) x y" & vbCrLf & _
            "foo(1,2)" & vbCrLf & _
            "foo(""h"", ""y"")" & vbCrLf
        Dim after As String = _
            "1 2" & vbCrLf & _
            """hy""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Quote me test
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method5()
        Dim before As String = _
            "#define foo(x) #x" & vbCrLf & _
            "foo(1)" & vbCrLf
        Dim after As String = _
            """1""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Collapse side by side strings
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method6()
        Dim before As String = _
            "#define foo(x) #x" & vbCrLf & _
            """h""foo(1)""y""" & vbCrLf
        Dim after As String = _
            """h1y""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' ## test
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method7()
        Dim before As String = _
            "#define foo(x) x##__" & vbCrLf & _
            "foo(y)" & vbCrLf
        Dim after As String = _
            "y__" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Method8()
        Dim before As String = _
            "#define foo(x,y) x##y" & vbCrLf & _
            "foo(y,z)" & vbCrLf
        Dim after As String = _
            "yz" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Weird macro arguments
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method9()
        Dim before As String = _
                  "#define foo(x) x" & vbCrLf & _
                  "foo(y(0))" & vbCrLf
        Dim after As String = _
            "y(0)" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub Method10()
        Dim before As String = _
            "#define foo(x,y) x ## y" & vbCrLf & _
            "#define foo2(x,y) x## y" & vbCrLf & _
            "#define foo3(x,y) x ##y" & vbCrLf & _
            "foo(1,2)" & vbCrLf & _
            "foo2(3,4)" & vbCrLf & _
            "foo3(5,6)" & vbCrLf
        Dim after As String = _
            "12" & vbCrLf & _
            "34" & vbCrLf & _
            "56" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Recursive method calls
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method11()
        Dim before As String = _
            "#define inner(x) x" & vbCrLf & _
            "#define outer(x) inner(x)" & vbCrLf & _
            "outer(5)" & vbCrLf
        Dim after As String = _
            "5" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' A more complex recursive method call
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method12()
        Dim before As String = _
            "#define inner(x,y) x##y" & vbCrLf & _
            "#define outer(x,y) inner(x,y)" & vbCrLf & _
            "outer(1,2)" & vbCrLf
        Dim after As String = _
            "12" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Make sure that quoted strings used as method macro arguments are properly
    ''' replaced in the string and collapsed
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method13()
        Dim before As String = _
            "#define x(y) ""foo"" y ""bar""" & vbCrLf & _
            "x(""hey"")" & vbCrLf
        Dim after As String = _
            """fooheybar""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Spacing between arguments needs to be maintained
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method14()
        Dim before As String = _
            "#define x(y) y" & vbCrLf & _
            "x(a b)" & vbCrLf
        Dim after As String = _
            "a b" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' When several items are passed as a single macro parameter make sure they still
    ''' go through replacement
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Method15()
        Dim before As String = _
            "#define foo bar" & vbCrLf & _
            "#define m1(x) x" & vbCrLf & _
            "m1(foo 2)" & vbCrLf
        Dim after As String = _
            "bar 2" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Side by strings should collapse
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Misc1()
        Dim before As String = _
            """foo""""bar""" & vbCrLf
        Dim after As String = _
            """foobar""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    ''' <summary>
    ''' Side by side strings with spaces should collapse
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub Misc2()
        Dim before As String = _
            """foo""     ""bar""" & vbCrLf
        Dim after As String = _
            """foobar""" & vbCrLf
        VerifyNormal(before, after)
    End Sub

    <TestMethod()> _
    Public Sub UnbalancedConditional1()
        Dim text As String = _
            "#ifndef WINAPI"
        VerifyCount(text, 0, 0)
    End Sub

    <TestMethod()> _
    Public Sub UnbalancedConditional2()
        Dim text As String = _
            "#ifdef WINAPI"
        VerifyCount(text, 1, 0)
    End Sub

    ''' <summary>
    ''' Make sure that permanent macros are preserved
    ''' </summary>
    ''' <remarks></remarks>
    <TestMethod()> _
    Public Sub PermanentMacro1()
        Dim list As New List(Of Macro)
        list.Add(New Macro("FOO", "BAR", True))
        VerifyNormal( _
            list, _
            "#define FOO BAZ" & vbCrLf & _
            "FOO" & vbCrLf, _
            "BAR" & vbCrLf)
    End Sub

End Class
